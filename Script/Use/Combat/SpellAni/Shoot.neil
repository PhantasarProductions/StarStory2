// License Information:
// src/Tricky Script/Script/Use/Combat/SpellAni/Shoot.neil
// Version: 22.05.08
// Copyright (C) 2022 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
#use "libs/linkedlist"

Module Class
	Static ReadOnly Var Img = Image.LoadNew("GFX/Combat/SpellAni/Laser.png","SPELLANI_LASER")
	Static ReadOnly Var Aud = Audio.LoadNew("Audio/Combat/SpellAni/photon.ogg","SPELLANI_LASER")
	Static ReadOnly Var Lst = new LinkedList()
	Static ReadOnly Delegate Update = NIKS
	
	Static Void Start(Act,string Param)
		Lst.Clear()
		For i,T in ipairs(Act.Targets_Tag)
			var beam = new Self(Act.Targets[i],Act.Executor)
			if !beam.afgekeurd
				Lst.AddLast(beam)
			end
		End
		Aud.Play()
	End
	
	Const Int MSpd=24
	
	ReadOnly Int R
	ReadOnly Int G
	ReadOnly Int B
	
	ReadOnly Int SX
	ReadOnly Int SY
	ReadOnly Int EX
	ReadOnly Int EY
		
	ReadOnly Number MX
	ReadOnly Number MY

	Number NX
	Number NY

	Get Int X
		Return math.floor(NX)
	End
	
	Get Int Y
		Return math.floor(NY)
	End
	
	Get Bool Afgekeurd
		Return SX==EX && SY==EY
	End
	
	Constructor(Tar,Exe)
		SX = Exe.X
		SY = Exe.Y - (Exe.H div 2)
		EX = Tar.X
		EY = Tar.Y - (Tar.H div 2)
		NX = SX
		NY = SY
		MX = (EX-SX) / MSPD
		MY = (EY-SY) / MSPD
		Int CD=1000
		Repeat
			R = math.random(0,255)
			G = math.random(0,255)
			B = math.random(0,255)
			CD--
		Until (CD<0) || (R+G+B>=300 && R+G+B<750)
		CSayF("Generated beam (%d,%d) -> (%d,%d) Spd(%f,%f)",SX,SY,EX,EY,MX,MY)
	End
	
	Void DrawBeam()
		Graphics.Color(R,G,B)
		Img.Draw(X,Y)
		NX+=MX
		NY+=MY
	End
	
	Static Void Draw()
		For Beam,BLnk in Lst.Each
			Beam.DrawBeam()
			If Beam.X<0 || Beam.Y<0 || Beam.X>Screen.W || Beam.Y>Screen.H
				BLnk.UnLink()
			End
		End
	End
	
	Static Get Bool Ended
		Return Lst.Count<=0
	End
End